// This source code is subject to the terms of the Mozilla Public License 2.0 at https://mozilla.org/MPL/2.0/
//@version=5
strategy("Bolinger Bands Strategy",
     overlay=true,
     initial_capital=10000000,
     currency=currency.USD,
     default_qty_type = strategy.percent_of_equity,
     default_qty_value=100,
     commission_type=strategy.commission.cash_per_contract,
     commission_value=0.005, //Interactive Brokers rate
     process_orders_on_close = true)

import AlphaCentauri66367/PineTradingbotWebhook/10 as tv

source = close
length = input.int(20, minval=1)  // Shorten the length to make the Bollinger Bands more responsive to price movements.
mult = input.float(2.5, minval=0.001, maxval=50)  // Adjust the multiplier to widen/narrow the bands, accommodating the potentially greater volatility in futures.
[basis, upper, lower] = ta.bb(source, length, mult)


buyCondition = strategy.position_size == 0 and ta.crossover(close, lower)
sellCondition = strategy.position_size > 0 and ta.crossunder(close, upper)

plot(upper, 'BB upper', color = color.aqua)
plot(lower, 'BB lower', color = color.aqua)

webhookKey = "WebhookReceived:8c43d5"
orderRef='CME202403_BB1'
qty=4
if buyCondition
    msg = tv.makeWebhookJson(webhookKey, orderRef, 'strategy.entrylong', qty, contract='future', ticker='ES', exchange='CME', lastTradeDateOrContractMonth='202403')
    strategy.entry("BBandLE", strategy.long, stop=lower)
    alert(msg, alert.freq_once_per_bar_close)


bgcolor(buyCondition ? color.new(color.blue, 30) : na)
if sellCondition
    msg = tv.makeWebhookJson(webhookKey, orderRef, 'strategy.close', qty, contract='future', ticker='ES', exchange='CME', lastTradeDateOrContractMonth='202403')
    strategy.close("BBandLE")
    alert(msg, alert.freq_once_per_bar_close)

//  Moving Average Indicator {
fastAverage = ta.ema(close, 10)
slowAverage = ta.ema(close, 50)
plot(fastAverage, title = "EMA 10", color=color.red)
plot(slowAverage, title = "EMA 50", color=color.red)
// Show the moving average trend with a coloured background
backgroundColor = if close > fastAverage and fastAverage > slowAverage
    color.new(color.green, 85)
else if close < fastAverage and fastAverage < slowAverage
    color.new(color.red, 85)
else
    color.new(color.orange, 90)
bgcolor(backgroundColor, title="EMA Background")
